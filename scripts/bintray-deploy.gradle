project.ext.getDeploymentVersion = {
    if (!project.hasProperty('deploymentVersion')) {
        throw new RuntimeException("deploymentVersion not provided. You must specify a 'deploymentVersion' project variable (gradlew -PdeploymentVersion=..)")
    }
    String tmpDeploymentVersion = deploymentVersion.replaceFirst("v", "");
    if (!tmpDeploymentVersion.matches("\\d+\\.\\d+\\.\\d+")) {
        throw new RuntimeException("Version is not valid. Correct format is like 1.0.2 but was " + tmpDeploymentVersion)
    }
    return tmpDeploymentVersion;
}

project.ext.getVcsTag = {
    if (!project.hasProperty('vcsTag')) {
        throw new RuntimeException("vcsTag not provided. You must specify a 'vcsTag' project variable (gradlew -PvcsTag=..)")
    }
    return vcsTag;
}

bintrayUpload.doFirst {
    println "Deploying version ${getDeploymentVersion()} from vscTag ${vcsTag()}"
}

bintray {
    user = "${BINTRAY.userName}"
    key = "${BINTRAY.apiToken}"

    publications = ['nebula']
    // When uploading Maven-based publication files. Nebula is because we're using the netflix plugins and they use nebula as default name for a publication
    dryRun = TRAVIS_CI // Whether to run this as dry-run, without deploying
    publish = false // Whether version should be auto published after an upload
    override = false // Whether to override version artifacts already published

    pkg {
        repo = "${BINTRAY.repo}"
        name = "${BINTRAY.name}"
        userOrg = "${BINTRAY.organization}"
        websiteUrl = "${INFO.github}"
        issueTrackerUrl = "${INFO.githubIssues}"
        vcsUrl = "${INFO.repo}"
        licenses = ['Apache-2.0']

        version {
            name = getDeploymentVersion()
            released = new Date()
            vcsTag = getVcsTag()
            gpg { sign = true }
        }
    }
}

def pomExtraInfo = {
    url INFO.url
    scm {
        connection "scm:git:${INFO.repo}"
        developerConnection "scm:git:${INFO.repo}"
        url "${INFO.repo}"
    }
    licenses {
        license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }
}

publishing {
    publications {
        nebula(MavenPublication) {
            groupId POM.groupId
            artifactId POM.artifactId
            version getDeploymentVersion()

            description POM.description

            artifact sourcesJar
            artifact javadocJar

            pom.withXml { asNode().children().last() + pomExtraInfo }
        }
    }
}
