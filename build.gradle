buildscript {
    project.ext.CERN_VM = System.getProperty('CERN_TECHNET_VM')
    project.ext.TRAVIS_CI = System.getProperty('TRAVIS')
    project.ext.POM = [
            groupId   : 'streamingpool',
            artifactId: 'streamingpool-core',
            description: 'This project is an high level abstraction over Reactive Streams libraries that is currently used inside CERN.'
    ]
    project.ext.INFO = [
            repo: 'https://github.com/streamingpool/streamingpool-core.git',
            url: 'http://www.streamingpool.org/',
            github: 'https://github.com/streamingpool/streamingpool-core',
            githubIssues: 'https://github.com/streamingpool/streamingpool-core/issues'
    ]
    project.ext.BINTRAY = [
            repo: 'streamingpool-core',
            name: 'streamingpool-repos',
            organization: 'streamingpool',
            userName: 'streamingpool-dev',
            apiToken: System.getProperty('BINTRAY_API_TOKEN')
    ]
    repositories {
        if (CERN_VM) {
            maven { url 'http://artifactory.cern.ch/repo' }
            maven { url 'http://artifactory.cern.ch/jcenter' }
        } else {
            jcenter()
        }
    }
}

plugins {
    id 'java'
    id 'jacoco'
    id 'maven'
    id 'maven-publish'
    id 'nebula.maven-publish' version '5.1.0'
    id 'com.jfrog.bintray' version '1.7.3'
}

repositories {
    if (CERN_VM) {
        maven { url 'http://artifactory.cern.ch/repo' }
        maven { url 'http://artifactory.cern.ch/jcenter' }
    } else {
        jcenter()
    }
}

if (TRAVIS_CI) {
    apply from: './scripts/bintray-deploy.gradle'
}

group 'org.streamingpool'

sourceCompatibility = 1.8

dependencies {
    compile 'org.reactivestreams:reactive-streams:1.0.0'
    compile 'io.reactivex.rxjava2:rxjava:2.1.+'
    compile 'com.typesafe.akka:akka-stream_2.11:2.4.16'

    compile 'org.springframework:spring-core:4.3.9.RELEASE'
    compile 'org.springframework:spring-context:4.3.9.RELEASE'
    compile 'org.springframework:spring-test:4.3.9.RELEASE' // Testing utils in /src/java for the moment

    compile 'org.slf4j:slf4j-api:1.7.+'

    compile 'com.google.guava:guava:22.0'

    compile 'junit:junit:4.12' // Testing utils in /src/java for the moment
    compile 'org.mockito:mockito-core:2.8.+' // Testing utils in /src/java for the moment
    testCompile 'org.assertj:assertj-core:3.8.+'
}

sourceSets {
    main {
        java {
            srcDir 'src/java'
        }
    }
    test {
        java {
            srcDir 'src/test'
        }
    }
}

javadoc { options.encoding = "UTF-8" }

task wrapper(type: Wrapper) { gradleVersion = '3.5' }

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts { archives javadocJar, sourcesJar }

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled true
        csv.enabled false
    }
}
